dependencies {
    def springVersion = "4.0.1.RELEASE"
    compile "org.springframework:spring-orm:${springVersion}"
    compile 'org.springframework.data:spring-data-jpa:1.5.1.RELEASE'

    compile "org.hibernate:hibernate-core:4.3.4.Final"
    compile 'org.hibernate:hibernate-entitymanager:4.3.4.Final'

    compile("com.mysema.querydsl:querydsl-core:$rootProject.ext.querydslVersion")
    compile("com.mysema.querydsl:querydsl-jpa:$rootProject.ext.querydslVersion")
    compile("com.mysema.querydsl:querydsl-sql:$rootProject.ext.querydslVersion")
    provided("com.mysema.querydsl:querydsl-apt:$rootProject.ext.querydslVersion") {
        exclude group: 'com.google.guava'
    }
    compile "org.springframework.data:spring-data-jpa:$rootProject.ext.springjpaVersion"
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.provided
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source sourceSets.generated.java.srcDirs.iterator().next()
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}
